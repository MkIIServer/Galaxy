plugins {
    id "java"
    id "idea"
    id "signing"
    id "org.jetbrains.kotlin.jvm" version "1.2.10"
    id "org.spongepowered.plugin" version "0.8.1"
    id "com.github.johnrengelman.shadow" version "2.0.2"
}

group "one.oktw"
version "1.0-SNAPSHOT"

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://repo.lucko.me/" }
}

ext.spongeVersion = "7.0.0-SNAPSHOT"
ext.CoroutinesVersion = "0.21"

dependencies {
    compile "org.spongepowered:spongeapi:$spongeVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$CoroutinesVersion"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$CoroutinesVersion"
    compile "org.mongodb:mongodb-driver:3.6.1"
    compile "com.github.NucleusPowered.Nucleus:nucleus-api:1.2.0-PR6"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    classifier = null
    exclude "META-INF/*"
    dependencies {
        include dependency("org.mongodb:")
        include dependency("org.jetbrains.kotlin:")
        include dependency("org.jetbrains.kotlinx:")
    }

    relocate "com.mongodb", "one.oktw.relocate.com.mongodb"
    relocate "org.bson", "one.oktw.relocate.org.bson"
    relocate "kotlin", "one.oktw.relocate.kotlin"
    relocate "kotlinx", "one.oktw.relocate.kotlinx"
}

signing {
    sign configurations.archives
}

jar.dependsOn(shadowJar)
jar.enabled = false